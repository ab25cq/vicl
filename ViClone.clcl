
class ViClone
{
    endOfAplication:bool;
    cursorX:int;
    cursorY:int;
    scrollTopY:int;
    scrollTopX:int;
    mode: int;
    keyEvents: lambda[](ViClone, int);
    fileName:String;
    startPositionOfInsertMode:int;
    tabSize:int;

    texts:EqualableList<String>;

    enum { EDITMODE, INSERTMODE, COMMANDMODE }

    def initialize() {
        endOfAplication = false;
        cursorX = 0;
        cursorY = 0;
        scrollTopY = 0;
        scrollTopX = 0;
        startPositionOfInsertMode = 0;
        tabSize = 4;

        mode = EDITMODE;

        keyEvents = new lambda[KEY_MAX](ViClone, int)();

        ### read file ###
        if(Clover.ARGV.length() == 2) {
            texts = EqualableList<String>();
        }
        elif(Clover.ARGV.length() == 3) {
            fname := Clover.ARGV.items(2);
            texts = fname.toPath().read().toString().split(/\n/);
            fileName = fname.clone();
        }
        else {
            throw Exception("vi clone can't edit multiple files");
        }

        ### start cursesw ###
        initscr();
        keypad(stdscr, true);
        noecho();

        ### default key events ###
        defaultKeyEnvents();
    }

    def finalize() {
        endwin();
    }

    def getLineInsideScreenSize(original_line: String, cursor_line:bool):Tuple2<String,Bool> {
        maxx := getmaxx();

        start_line := 0;
        if(cursor_line) {
            start_line = scrollTopX;
        }

        result := "";
        n := 0;
        for(i:=start_line; i<original_line.length(); i++) {
            c := original_line.chars(i);

            if(c == '\t') {
                if(n >= maxx-8-1) {
                    break;
                }

                result.append(">       ");
                n += 8;
            }
            else {
                if(n >= maxx-1) {
                    break;
                }

                result.append(c);
                n++;
            }
        }

        over_maxx := i != original_line.length();

        return (result, over_maxx);
    }

    def getLine(original_line:String):Tuple2<String,Bool> {
        result := "";
        for(i:=0; i<original_line.length(); i++) {
            c := original_line.chars(i);

            if(c == '\t') {
                result.append(">       ");
            }
            else {
                result.append(c);
            }
        }

        maxx := getmaxx();

        return (result, result.length() >= maxx-1);
    }

    def fixCursorAndScrollTop() {
        maxy := getmaxy()-1;

        if(cursorY >= maxy) {
            scroll_size := cursorY - maxy + 1;

            scrollTopY += scroll_size;
            cursorY -= scroll_size;

            if(scrollTopY >= texts.length()) {
                scrollTopY = texts.length()-1;
            }

            if(scrollTopY < 0) {
                scrollTopY = 0;
            }
        }

        if(cursorY < 0) {
            scroll_size := -cursorY;

            cursorY = 0;
            scrollTopY -= scroll_size;

            if(scrollTopY >= texts.length()) {
                scrollTopY = texts.length()-1;
            }

            if(scrollTopY < 0) {
                scrollTopY = 0;
            }
        }

        if(cursorY < 0) {
            cursorY = 0;
        }

        if(cursorY >= maxy) {
            cursorY = maxy -1;
        }

        if(scrollTopY + maxy >= texts.length()) {
            maxy2 := texts.length() - scrollTopY;

            if(cursorY >= maxy2) {
                cursorY = maxy2-1;
            }
        }

        original_line := texts.items(scrollTopY+cursorY);
        (line, over_maxx) := getLineInsideScreenSize(original_line, false);

        maxx := getmaxx();

        if(line.equals("")) {
            cursorX = 0;
        }
        elif(cursorX >= maxx-1) {
            if(over_maxx) {
                scroll_size := cursorX - maxx + 1;

                cursorX -= scroll_size + 1;
                scrollTopX += scroll_size + 1;

                if(scrollTopX >= original_line.length()-maxx+1) {
                    scrollTopX = original_line.length()-maxx+1;
                }
            }
            else {
                cursorX = line.length()-1;
            }
        }
        elif(cursorX >= line.length()) {
            cursorX = line.length()-1;
        }
    }

    def enterNewLine() {
        maxx := getmaxx();
        maxy := getmaxy();

        original_line := texts.items(scrollTopY+cursorY);

        spaces_num := 0;
        try {
            spaces_num = original_line.scan(/^\s+/).items(0).length() + 1;
            cursorX = spaces_num + 1;
        } catch(e:Exception) {
            spaces_num = -1;
            cursorX = 0;
        }

        scrollTopX = 0;
        cursorY++;
        
        spaces := " ".multiply(spaces_num);
        texts.insert(cursorY+scrollTopY, spaces);

        fixCursorAndScrollTop();

        startPositionOfInsertMode = cursorX + scrollTopX;
    }

    def defaultKeyEnvents() {
        keyEvents['Z'.to_int] = lambda(self:ViClone, key:int) {
            endOfAplication = true;
        }
        keyEvents['j'.to_int] = lambda(self:ViClone, key:int) {
            cursorY++;

            maxy := getmaxy()-1;

            if(cursorY >= maxy) {
                scrollTopY++;
                cursorY--;
            }

            if(scrollTopY + cursorY >= texts.length()) {
                scrollTopY--;
                cursorY--;

                if(scrollTopY < 0) {
                    scrollTopY = 0;
                }
                if(cursorY < 0) {
                    cursorY = 0;
                }
            }

            original_line := texts.items(scrollTopY+cursorY);
            (line, over_maxx) := getLineInsideScreenSize(original_line, false);

            if(line.equals("")) {
                cursorX = 0;
            }
            else {
                if(cursorX >= line.length()) {
                    cursorX = line.length()-1;
                }
            }

            scrollTopX = 0;
        }
        keyEvents['k'.to_int] = lambda(self:ViClone, key:int) {
            cursorY--;

            maxy := getmaxy()-1;

            if(cursorY < 0) {
                scrollTopY--;
                cursorY++;
            }

            if(scrollTopY < 0) {
                scrollTopY = 0;
            }

            original_line := texts.items(scrollTopY+cursorY);
            (line, over_maxx) := getLineInsideScreenSize(original_line, false);

            if(line.equals("")) {
                cursorX = 0;
            }
            else {
                if(cursorX >= line.length()) {
                    cursorX = line.length()-1;
                }
            }

            scrollTopX = 0;
        }
        keyEvents['h'.to_int] = lambda(self:ViClone, key:int) {
            cursorX--;

            if(cursorX < 0) {
                scrollTopX -= -cursorX;

                if(scrollTopX < 0) {
                    scrollTopX = 0;
                }

                cursorX = 0;
            }
        }
        keyEvents['l'.to_int] = lambda(self:ViClone, key:int) {
            self.cursorX++;

            maxx := getmaxx();

            original_line := texts.items(scrollTopY+cursorY);
            (line, over_maxx) := getLineInsideScreenSize(original_line, false);

            if(line.equals("")) {
                cursorX = 0;
            }
            elif(cursorX >= maxx-1) {
                if(over_maxx) {
                    cursorX--;
                    scrollTopX++;

                    if(scrollTopX >= original_line.length()-maxx+1) {
                        scrollTopX = original_line.length()-maxx+1;
                    }
                }
                else {
                    cursorX = line.length()-1;
                }
            }
            elif(cursorX >= line.length()) {
                cursorX = line.length()-1;
            }
        }
        keyEvents['w'.to_int] = lambda(self:ViClone, key:int) {
            original_line := texts.items(scrollTopY+cursorY);

            (line,over_maxx) := getLine(original_line);

            if(original_line.identifyWith(null).negative()) {
                if(line.equals("")) {
                    cursorX = 0;
                    cursorY++;
                    scrollTopX = 0;
                }
                else {
                    c := line.chars(scrollTopX+cursorX);

                    if(c.isalpha()) {
                        while(c.isalpha()) {
                            cursorX++;

                            if(scrollTopX+cursorX >= line.length()-1) {
                                cursorX = 0;
                                cursorY++;
                                scrollTopX = 0;
                                break;
                            }

                            c = line.chars(scrollTopX+cursorX);
                        }
                    }
                    elif(c.isdigit()) {
                        while(c.isdigit()) {
                            cursorX++;

                            if(scrollTopX+cursorX >= line.length()-1) {
                                cursorX = 0;
                                cursorY++;
                                scrollTopX = 0;
                                break;
                            }

                            c = line.chars(scrollTopX+cursorX);
                        }
                    }
                    elif(c.isspace()) {
                        while(c.isspace()) {
                            cursorX++;

                            if(scrollTopX+cursorX >= line.length()-1) {
                                cursorX = 0;
                                cursorY++;
                                scrollTopX = 0;
                                break;
                            }

                            c = line.chars(scrollTopX+cursorX);
                        }
                    }
                    else {
                        while(!c.isalpha() && !c.isdigit() && !c.isspace()) {
                            cursorX++;

                            if(scrollTopX+cursorX >= line.length()-1) {
                                cursorX = 0;
                                cursorY++;
                                scrollTopX = 0;
                                break;
                            }

                            c = line.chars(scrollTopX+cursorX);
                        }
                    }
                }

                maxx := getmaxx();

                if(line.equals("")) {
                    cursorX = 0;
                }
                elif(cursorX >= maxx-1) {
                    if(over_maxx) {
                        if(scrollTopX >= original_line.length()-maxx+1) {
                            cursorX = 0;
                            cursorY++;
                            scrollTopX = 0;
                        }
                        else {
                            scroll_size := cursorX - maxx + 1;

                            cursorX-=scroll_size + 1;
                            scrollTopX+=scroll_size + 1;

                            if(scrollTopX >= original_line.length()-maxx+1) {
                                scrollTopX = original_line.length()-maxx+1;
                            }
                        }
                    }
                    else {
                        cursorX = line.length()-1;
                    }

                    if(cursorX >= maxx-1) {
                        cursorX = 0;
                        scrollTopX = 0;
                        cursorY++;
                    }
                }
                elif(cursorX >= line.length()) {
                    cursorX = line.length()-1;
                    scrollTopX = 0;
                }

                maxy := getmaxy()-1;

                if(cursorY >= maxy) {
                    scrollTopY++;
                    cursorY--;
                }

                if(scrollTopY + cursorY >= texts.length()) {
                    scrollTopY--;
                    cursorY--;

                    if(scrollTopY < 0) {
                        scrollTopY = 0;
                    }
                    if(cursorY < 0) {
                        cursorY = 0;
                    }
                }
            }
        }
        keyEvents['b'.to_int] = lambda(self:ViClone, key:int) {
            original_line := texts.items(scrollTopY+cursorY);

            (line,over_maxx) := getLine(original_line);

            if(original_line.identifyWith(null).negative()) {
                if(line.equals("")) {
                    cursorX = 9999;
                    cursorY--;
                }
                else {
                    c := line.chars(scrollTopX+cursorX);

                    if(c.isalpha()) {
                        while(c.isalpha()) {
                            cursorX--;

                            if(scrollTopX+cursorX < 0) {
                                if(cursorY == 0) {
                                    cursorX = 0;
                                    scrollTopX = 0;
                                }
                                else {
                                    cursorX = 9999;
                                    cursorY--;
                                    scrollTopX = 0;
                                }
                                break;
                            }

                            c = line.chars(scrollTopX+cursorX);
                        }
                    }
                    elif(c.isdigit()) {
                        while(c.isdigit()) {
                            cursorX--;

                            if(scrollTopX+cursorX < 0) {
                                if(cursorY == 0) {
                                    cursorX = 0;
                                    scrollTopX = 0;
                                }
                                else {
                                    cursorX = 9999;
                                    cursorY--;
                                    scrollTopX = 0;
                                }
                                break;
                            }

                            c = line.chars(scrollTopX+cursorX);
                        }
                    }
                    elif(c.isspace()) {
                        while(c.isspace()) {
                            cursorX--;

                            if(scrollTopX+cursorX < 0) {
                                if(cursorY == 0) {
                                    cursorX = 0;
                                    scrollTopX = 0;
                                }
                                else {
                                    cursorX = 9999;
                                    cursorY--;
                                    scrollTopX = 0;
                                }
                                break;
                            }

                            c = line.chars(scrollTopX+cursorX);
                        }
                    }
                    else {
                        while(!c.isalpha() && !c.isdigit() && !c.isspace()) {
                            cursorX--;

                            if(scrollTopX+cursorX < 0) {
                                if(cursorY == 0) {
                                    cursorX = 0;
                                    scrollTopX = 0;
                                }
                                else {
                                    cursorX = 9999;
                                    cursorY--;
                                    scrollTopX = 0;
                                }
                                break;
                            }

                            c = line.chars(scrollTopX+cursorX);
                        }
                    }
                }

                if(cursorX < 0) {
                    scrollTopX -= -cursorX;
                    cursorX = 0;
                }

                maxx := getmaxx();

                original_line = texts.items(scrollTopY+cursorY);
                (line, over_maxx) = getLineInsideScreenSize(original_line, false);

                if(line.equals("")) {
                    cursorX = 0;
                }
                elif(cursorX >= maxx-1) {
                    if(over_maxx) {
                        scroll_size := cursorX - maxx + 1;

                        cursorX -= scroll_size + 1;
                        scrollTopX += scroll_size + 1;

                        if(scrollTopX >= original_line.length()-maxx+1) {
                            scrollTopX = original_line.length()-maxx+1;
                        }
                    }
                    else {
                        cursorX = line.length()-1;
                    }
                }
                elif(cursorX >= line.length()) {
                    cursorX = line.length()-1;
                }

                if(cursorY < 0) {
                    scrollTopY--;
                    cursorY = 0;
                }

                if(scrollTopY < 0) {
                    scrollTopY = 0;
                }

                if(scrollTopY + cursorY >= texts.length()) {
                    scrollTopY-= scrollTopY + cursorY - texts.length();
                }
            }
        }
        keyEvents['0'.to_int] = lambda(self:ViClone, key:int) {
            cursorX = 0;
            scrollTopX = 0;
        }
        keyEvents['$'.to_int] = lambda(self:ViClone, key:int) {
            maxx := getmaxx();

            original_line := texts.items(scrollTopY+cursorY);
            (line, over_maxx) := getLineInsideScreenSize(original_line, false);

            if(line.equals("")) {
                cursorX = 0;
            }
            elif(over_maxx) {
                cursorX = line.length()-1;
                scrollTopX = original_line.length()-maxx+1;
            }
            else {
                cursorX = line.length()-1;
            }
        }

        ### Controll Keys ###
        keyEvents[('D'-'A').to_int+1] = lambda(self:ViClone, key:int) {  # Control-D
            maxy := getmaxy()-1;
            self.cursorY += maxy / 2;

            scrollTopX = 0;

            fixCursorAndScrollTop();

            clear();
            view();
        }
        keyEvents[('U'-'A').to_int+1] = lambda(self:ViClone, key:int) {  # Control-U
            maxy := getmaxy()-1;
            self.cursorY -= maxy / 2;

            scrollTopX = 0;

            fixCursorAndScrollTop();

            clear();
            view();
        }
        keyEvents[('L'-'A').to_int+1] = lambda(self:ViClone, key:int) {  # Control-L
            clear();
            view();
        }

        ### Insert Mode ###
        keyEvents['i'.to_int] = lambda(self:ViClone, key:int) {
            mode = INSERTMODE;
            startPositionOfInsertMode = cursorX + scrollTopX;
        }

        keyEvents['a'.to_int] = lambda(self:ViClone, key:int) {
            maxx := getmaxx();

            cursorX++;

            if(cursorX >= maxx-1) {
                scrollTopX++;
                cursorX--;
            }

            mode = INSERTMODE;
            startPositionOfInsertMode = cursorX + scrollTopX;
        }
        keyEvents['A'.to_int] = lambda(self:ViClone, key:int) {
            maxx := getmaxx();

            original_line := texts.items(scrollTopY+cursorY);
            (line,over_max) := getLine(original_line);

            if(line.length() > maxx-1) {
                scrollTopX = line.length() - maxx +2;
                cursorX = maxx-2;
            }
            else {
                cursorX = line.length();
            }

            mode = INSERTMODE;
            startPositionOfInsertMode = cursorX + scrollTopX;
        }
        keyEvents['o'.to_int] = lambda(self:ViClone, key:int) {
            enterNewLine();

            mode = INSERTMODE;
            startPositionOfInsertMode = cursorX + scrollTopX;
        }
    }

    def input(): int {
        return getch();
    }

    def insertMode(key:int) {
        when(key) {
            case (('['-'A').to_int+1) {
                mode = EDITMODE;
                fixCursorAndScrollTop();
            }

            ### Backspace ###
            case (('H'-'A').to_int+1, KEY_BACKSPACE) {
                maxx := getmaxx();
                original_line := texts.items(cursorY + scrollTopY);
                (line,over_max) := getLine(original_line);

                if(original_line.identifyWith(null).negative()) {
                    try {
                        if(scrollTopX+cursorX > 0 && cursorX + scrollTopX > startPositionOfInsertMode) {
                            original_line.chars(cursorX + scrollTopX);

                            cursorX--;
                            original_line.delete(cursorX + scrollTopX);

                            if(cursorX < 0) {
                                scrollTopX--;
                                cursorX++;

                                if(scrollTopX < 0) {
                                    scrollTopX = 0;
                                    cursorX = 0;
                                }
                            }
                        }
                    }
                    catch(e:Exception) {
                        fixCursorAndScrollTop();
                    }
                }
            }

            case ('\n'.to_int) {
                enterNewLine();
            }

            case (('D'-'A').to_int+1) {
                maxx := getmaxx();
                original_line := texts.items(cursorY + scrollTopY);
                (line,over_max) := getLine(original_line);

                if(original_line.identifyWith(null).negative()) {
                    try {
                        if(scrollTopX+cursorX > 0) {
                            cursorX-=4;
                            original_line.delete((cursorX + scrollTopX)..(cursorX + scrollTopX+4));

                            if(cursorX < 0) {
                                scrollTopX--;
                                cursorX++;

                                if(scrollTopX < 0) {
                                    scrollTopX = 0;
                                    cursorX = 0;
                                }
                            }
                        }
                    }
                    catch(e:Exception) {
                        fixCursorAndScrollTop();
                    }
                }
                
            }

            case ('\t'.to_int) {
                maxx := getmaxx();
                line := texts.items(cursorY + scrollTopY);

                if(line.identifyWith(null).negative()) {
                    line.insert(cursorX + scrollTopX, " ".multiply(tabSize));
                    cursorX+=tabSize;

                    if(cursorX >= maxx-1) {
                        scrollTopX+=tabSize;
                        cursorX-=tabSize;
                    }
                }
            }

            else {
                maxx := getmaxx();
                line := texts.items(cursorY + scrollTopY);

                if(line.identifyWith(null).negative()) {
                    c := key.to_char;
                    if(c.isalnum() || c == ' ') {
                        line.insert(cursorX + scrollTopX, c.toString());
                        cursorX++;

                        if(cursorX >= maxx-1) {
                            scrollTopX++;
                            cursorX--;
                        }
                    }
                }
            }
        }
    }

    def runKeyInvent(key:int) {
        when(mode) {
            case (EDITMODE) {
                if(keyEvents[key].identifyWith(null).negative()) {
                    keyEvents[key](self, key);
                }
            }

            case (INSERTMODE) {
                insertMode(key);
            }

            case (COMMANDMODE) {
            }
        }
    }

    def clearWithoutBlink() {
        maxx := getmaxx();
        maxy := getmaxy();

        line := " ".multiply(maxx);
        line2 := " ".multiply(maxx-1);

        for(i:=0; i<maxy; i++) {
            if(i == maxy -1) {
                line2.mvprintw(i, 0, "%s");
            }
            else {
                line.mvprintw(i, 0, "%s");
            }
        }
    }

    def view() {
        clearWithoutBlink();

        maxx := getmaxx();
        maxy := getmaxy();

        visible_texts := texts.subList(scrollTopY, scrollTopY + maxy-1);

        y := 0;
        visible_texts.each {
            (line, over_maxx) := getLineInsideScreenSize(it, cursorY == y);

            ### cursor ####
            if(cursorY == y) {
                if(line.length() == 0) {
                    attron(A_REVERSE);
                    " ".mvprintw(y,0, "%s");
                    attroff(A_REVERSE);
                }
                else {
                    line.subString(0, cursorX).mvprintw(y, 0, "%s");

                    c := line.chars(cursorX);

                    attron(A_REVERSE);
                    c.toString().mvprintw(y, cursorX, "%s");
                    attroff(A_REVERSE);

                    line.subString(cursorX+1, -1).mvprintw(y, cursorX+1, "%s");
                }
            }
            ### visible line ###
            else {
                if(line.length() != 0) {
                    line.mvprintw(y, 0, "%s");
                }
            }

            y++;
        }

        attron(A_REVERSE);
        mvprintw(maxy-1, 0, "%s %d(%d%%)", array { fileName, scrollTopY+cursorY, scrollTopY+cursorY/texts.length() });
        attroff(A_REVERSE);

        refresh();
    }
}
